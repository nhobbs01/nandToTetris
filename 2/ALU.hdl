// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // 1. zx => zero the x input
    Mux16(a=x, b=false, sel=zx, out=zeroX);
    // 2. nx => negate the x input
    Not16(in=zeroX, out=negateX);
    Inc16(in=negateX,out=twoComplementX);
    Mux16(a=zeroX, b=negateX, sel=nx, out=processedX);
    // 3. zy => zero the y input
    Mux16(a=y, b=false, sel=zy, out=zeroY);
    // 4. ny => negate the y input
    Not16(in=zeroY, out=negateY);
    Mux16(a=zeroY, b=negateY, sel=ny, out=processedY);

    // 5. f == 1 : x+y, f==0: x&y
    Add16(a=processedX, b=processedY, out=xPlusy);
    And16(a=processedX, b=processedY, out=xAndy);
    Mux16(a=xAndy, b=xPlusy, sel=f, out=funOut);

    // 6. no: negate output
    Not16(in=funOut, out=negateOut);

    Mux16(a=funOut, b=negateOut, sel=no, out=out, out[15]=ng, out[0..7]=out1, out[8..15]=out2);
    
    // Compute zero register
    Or8Way(in=out1,out=orOut1);
    Or8Way(in=out2,out=orOut2);
    Or(a=orOut1, b=orOut2, out=OrOut);
    Not(in=OrOut, out=zr);

}